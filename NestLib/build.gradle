buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.+'
    }
}

apply plugin: 'android-library'

android {
    compileSdkVersion 19
    buildToolsVersion "19.1.0"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            runProguard false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    libraryVariants.all { variant ->
        def javadocName = "generate${variant.name.capitalize()}Javadoc"
        def jarName = "generate${variant.name.capitalize()}JavadocJar"

        task(javadocName, type: Javadoc, dependsOn: variant.javaCompile) {
            source = variant.javaCompile.source
            ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
            classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)
            exclude '**/BuildConfig.java', '**/R.java'
            mustRunAfter variant.javaCompile
        }

        task(jarName, type: Jar, dependsOn: javadocName) {
            destinationDir = new File("${project.buildDir}/outputs")
            archiveName "nestdk-javadoc-${variant.mergedFlavor.versionName}.jar"
            classifier = 'javadoc'
            from tasks.getByName(javadocName).destinationDir
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile fileTree(dir: 'third_party', include: ['*.jar'])
}
